group 'com.checkout.flutter.checkout_flutter_bridge'
version '1.0'

buildscript {
    ext.kotlin_version = '2.1.0'
    ext.checkout_version = '1.2.0'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // Classpath is handled by the host app
    }
}

// Helper to allow host app to override versions
def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        // Required for Checkout.com Risk SDK dependencies
        maven { url = uri("https://jitpack.io") }
        maven { url = uri("https://maven.fpregistry.io/releases") }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

android {
    compileSdk safeExtGet('compileSdkVersion', 35)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        compose = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdk safeExtGet('minSdkVersion', 21)
    }

    namespace 'com.checkout.flutter.checkout_flutter_bridge'
}

dependencies {
    // Checkout.com SDK
    implementation "com.checkout:checkout-android-components:1.2.0"
    
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.0"
    
    // Lifecycle + ViewModel
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.7"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7"
    implementation "androidx.activity:activity-ktx:1.10.1"
    implementation "androidx.activity:activity-compose:1.10.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7"
    implementation "androidx.savedstate:savedstate:1.2.1"
    
    // Compose BOM
    implementation platform("androidx.compose:compose-bom:2025.02.00")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.compose.material3:material3"
    
    // Google Play Services
    implementation "com.google.android.gms:play-services-wallet:19.4.0"
    implementation "com.google.android.gms:play-services-base:18.6.0"
}
